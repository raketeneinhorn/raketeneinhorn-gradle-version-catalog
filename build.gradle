plugins {
    id 'version-catalog'
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.18.3'
    id 'org.sonarqube' version '5.1.0.4882'
}

group 'com.raketeneinhorn.gradle'
version = scmVersion.version

catalog {
    versionCatalog {
        version('spring-boot', '3.3.2')
        version('lombok', '8.6')

        library('spring-boot-gradle-plugin', 'org.springframework.boot', 'spring-boot-gradle-plugin').versionRef('spring-boot')
        library('lombok-plugin', 'io.freefair.gradle', 'lombok-plugin').versionRef('lombok')

        plugin('axion-release', 'pl.allegro.tech.build.axion-release:1.18.3')
        plugin('sonarqube', 'org.sonarqube:5.1.0.4882')
    }
}

sonar {
    properties {
        property "sonar.projectKey", "raketeneinhorn_raketeneinhorn-gradle-version-catalog"
        property "sonar.organization", "raketeneinhorn"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

publishing {
    publications {
        raketeneinhornGradleVersionCatalog(MavenPublication) {
            from components.versionCatalog
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/raketeneinhorn/raketeneinhorn-gradle-version-catalog"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
