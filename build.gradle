plugins {
    id 'version-catalog'
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.21.1'
    id 'org.sonarqube' version '7.2.2.6593'
}

group = 'com.raketeneinhorn.gradle'
version = scmVersion.version

catalog {
    versionCatalog {
        version('spring-boot', '4.0.1')
        version('lombok', '9.1.0')

        /* bom */
        library('jmolecules-bom', 'org.jmolecules', 'jmolecules-bom').version('2025.0.2')

        library('dasniko-testcontainers-keycloak', 'com.github.dasniko', 'testcontainers-keycloak').version('4.0.1')

        plugin('axion-release', 'pl.allegro.tech.build.axion-release').version('1.21.1')
        plugin('sonarqube', 'org.sonarqube').version('7.2.2.6593')
        plugin('springframework-boot', 'org.springframework.boot').versionRef('spring-boot')
        plugin('lombok', 'io.freefair.lombok').versionRef('lombok')

        /* plugins as libraries */
        library('spring-boot-gradle-plugin', 'org.springframework.boot', 'spring-boot-gradle-plugin').versionRef('spring-boot')
        library('lombok-plugin', 'io.freefair.gradle', 'lombok-plugin').versionRef('lombok')
    }
}

sonar {
    properties {
        property "sonar.projectKey", "raketeneinhorn_raketeneinhorn-gradle-version-catalog"
        property "sonar.organization", "raketeneinhorn"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

publishing {
    publications {
        raketeneinhornGradleVersionCatalog(MavenPublication) {
            from components.versionCatalog
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/raketeneinhorn/raketeneinhorn-gradle-version-catalog"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
